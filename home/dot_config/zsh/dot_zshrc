# ~/.config/zsh/.zshrc

# Directory for ZSH config
zsh_dir=${${ZDOTDIR}:-$HOME/.config/zsh}

# If not running interactively, don't do anything
[[ $- != *i* ]] && return

# Load API keys from .env (for claude-mem worker and other tools)
if [[ -f "$HOME/.env" ]]; then
  export $(grep -v '^#' "$HOME/.env" | grep -v '^$' | xargs)
fi

# History configuration
export HISTFILE="${ZDOTDIR:-$HOME}/.zsh_history"
export HISTSIZE=20000
export SAVEHIST=$HISTSIZE
setopt HIST_IGNORE_DUPS   # Don't record duplicates
setopt HIST_REDUCE_BLANKS # Remove superfluous blanks
setopt INC_APPEND_HISTORY # Write immediately to history file
setopt HIST_NO_STORE      # Don't store history command itself
setopt HIST_IGNORE_SPACE  # Commands with leading space not added

# Source all ZSH config files (if present)
if [[ -d $zsh_dir ]]; then
  # Import alias files
  source ${zsh_dir}/aliases.zsh
  source ${zsh_dir}/homebrew.zsh
 fi

# Append Cargo to path, if it's installed
if [[ -d "$HOME/.cargo/bin" ]]; then
  export PATH="$HOME/.cargo/bin:$PATH"
fi

# Append Deno to path, if it's installed
if [[ -d "$HOME/.deno" ]]; then
  export DENO_INSTALL="$HOME/.deno"
  export PATH="$DENO_INSTALL/bin:$PATH"
fi

# Add Zoxide (for cd, quick jump) to shell
if hash zoxide 2> /dev/null; then
    eval "$(zoxide init zsh)"
fi

# Initialize Starship prompt, if it's installed
if hash starship 2> /dev/null; then
  export STARSHIP_CONFIG="$HOME/.config/starship/starship.toml"
  eval "$(starship init zsh)"
fi

# Activate mise for tool version management
if hash mise 2> /dev/null; then
  eval "$(mise activate zsh)"
fi

# Source Zsh plugins installed by Homebrew (if they exist)
local brew_prefix
brew_prefix=$(brew --prefix)

local plugins=(
  "$brew_prefix/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
  "$brew_prefix/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
)

for plugin_file in "${plugins[@]}"; do
  if [[ -f "$plugin_file" ]]; then
    source "$plugin_file"
  fi
done
